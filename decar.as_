WRKARE: EQU 8000H
NUML: EQU 4
NUM1L:EQU NUML
NUM2L:EQU NUML
NUMLEN:EQU NUML+1
NUM1: EQU WRKARE
NUM2: EQU WRKARE+10H
RES: EQU WRKARE+20H
RES1:EQU WRKARE+30H
RES2:EQU WRKARE+40H

  LXI H,TXT1
  CALL 0F818H
  LXI H,WRKARE+180H
  CALL INPSTR
  LXI H,WRKARE+180H
  LXI D,DATA
  CALL VAL

  LDA SMB
  PUSH PSW
  CALL READ
  LXI D,DATA1
  CALL VALNUM

   LXI H,DATA
  CALL ?PRNUM
  MVI C,' '
  CALL 0F809H
  LXI H,DATA1
  CALL ?PRNUM
  MVI C,'='
  CALL 0F809H
  POP PSW

  LXI H,DATA
  LXI D,DATA1
  LXI B,RES2
  CALL ?CHK
  LXI H,RES2
  CALL ?PRLN
  JMP 0F86CH

?CHK:
  CPI '*'
  JZ FMUL
  CPI '+'
  JZ FADD
  CPI '-'
  JZ FSUB
  CPI '/'
  JZ FDIV
  JMP ERROR

TXT1: DB 0DH,0AH,'NUM1>',0
TXT2: DB 0DH,0AH,'NUM2>',0
VAL:
 SHLD PNT
 CALL READ
 JMP VALNUM

READ:
 PUSH H
 LHLD PNT
 MOV A,M
 INX H
 SHLD PNT
 POP H
 STA SMB
 RET

FIG:
 LDA SMB
 SUI 30H
 RC
 CPI 0AH
 RNC
 CALL SETDIG
 INR C
 CALL READ
 JMP FIG

FIG1:
 LDA SMB
 SUI 30H
 RC
 CPI 0AH
 RNC
 CALL SETDIG
 CALL READ
 JMP FIG1


SETDIG:
 PUSH H
 MOV H,A
 MOV A,B
 INR B
 CPI NUML+NUML
 JZ ERROR
 ANI 1
 JNZ SET0
 MOV A,H
 RRC
 RRC
 RRC
 RRC
 MOV H,A
 LDAX D
 ANI 0FH
 ORA H
 STAX D
 JMP SET1
SET0:
 LDAX D
 ANI 0F0H
 ORA H
 STAX D
 INX D
SET1:
 POP H
 RET


VALNUM:
 PUSH D
 MVI C,0
 MVI B,0
 LDA SMB
 CPI '-'
 JNZ VLN12
 MVI C,80H
VLN10:
 CALL READ
 JMP VLN11
VLN12:
 CPI '+'
 JZ VLN10
VLN11:
 CALL FIG
 LDA SMB
 CPI '.'
 JNZ VLN1
 CALL READ
 CALL FIG1
VLN1:
 MVI A,NUML+NUML
 CMP B
 JZ VLN2
 XRA A
 CALL SETDIG
 JMP VLN1
VLN2:
 MVI B,0
 LDA SMB
 CPI 'E'
 JNZ VLN5
 CALL READ
 LDA SMB
 CPI '+'
 JZ VLN30
 CPI '-'
 JNZ VLN3
 MOV A,C
 ORI 40H
 MOV C,A
VLN30:
 CALL READ
VLN3:
 LDA SMB
 SUI 30H
 JC ERROR
 CPI 0AH
 JNC ERROR
 MOV B,A
 CALL READ
 LDA SMB
 SUI 30H
 JC VLN6
 CPI 0AH
 JNC VLN6
 MOV A,B
 RRC
 RRC
 RRC
 RRC
 MOV B,A
 LDA SMB
 SUI 30H
 ORA B
 MOV B,A
VLN6:
 MOV A,B
 CALL DEC_BIN
VLN5:
 MOV A,C
 ANI 40H
 JZ VLN4
 MOV A,B
 CMA
 INR A
 MOV B,A
VLN4:
 MOV A,C
 ANI 3FH
 ADD B
 ANI 3FH
 MOV B,A
 MOV A,C
 ANI 0C0H
 ORA B
 STAX D
 POP D
 RET

INPSTR:
 PUSH H
 MVI B,1
INP0:
 CALL 0F803H
 CPI 7FH
 JNZ SYM
 DCR B
 JNZ SYMLT
 MVI B,1
 JMP INP0
SYMLT:
 DCX H
 PUSH H
 LXI H,CURLT
 CALL 0F818H
 POP H
 JMP INP0
CURLT:
 DB 8,20H,8,0
SYM:
 CPI 0DH
 JZ _CR
 CPI 3
 JZ 0F86CH
 CPI 20H
 JC INP0

 MOV M,A
 MOV C,A
 CALL 0F809H
 INR B
 INX H
 JMP INP0
_CR:
 MVI M,0
 POP H
 RET

FADD:
 PUSH B
 CALL UNPACK
 LDA ?EXP2
 MOV B,A
 LDA ?EXP1
 SUB B
 CM EXNUM
 LDA ?EXP2
 MOV B,A
 LDA ?EXP1
 STA ?EXP
 SUB B
 CPI NUML+NUML+2
 JNC A7
 MOV B,A
 INR B
A5:
 DCR B
 JZ A6
 PUSH B
 LXI H,NUM2
 MVI C,NUML+NUML+1
 CALL ?NBLRT
 POP B
 JMP A5
A6:
 XRA A
 STA ?SIGN
 LDA ?SIGN1
 ORA A
 LDA ?SIGN2
 JNZ A60
 ORA A
 JNZ A610
 CALL ADDMAN
 JMP A7
A610:
 CALL SUBSM
 JMP A7
A60:
 ORA A
 JNZ A600
 CALL SUBSM
 LDA ?SIGN
 XRI 80H
 STA ?SIGN
 JMP A7
A600:
 CALL ADDMAN
 LDA ?SIGN
 XRI 80H
 STA ?SIGN
 JMP A7
A70:
 LDA ?SIGN1
 STA ?SIGN
 LXI H,NUM1
 LXI D,RES+1
 MVI C,NUML+NUML+1
 MOV A,M
 STAX D
 INX H
 INX D
 DCR C
 JNZ $-5
A7:
 CALL NORMAL
 POP B
 CALL PACK
 RET

FSUB:
 XCHG
 CALL RNEG
 XCHG
 JMP FADD

RNEG:
 PUSH H
 PUSH B
 LXI B,NUML
 DAD B
 MOV A,M
 XRI 80H
 MOV M,A
 POP B
 POP H
 RET

EXNUM:
 LDA ?SIGN1
 MOV B,A
 LDA ?SIGN2
 STA ?SIGN1
 MOV A,B
 STA ?SIGN2
 LDA ?EXP1
 MOV B,A
 LDA ?EXP2
 STA ?EXP1
 MOV A,B
 STA ?EXP2
 MVI C,NUML+NUML
 LXI D,NUM1
 LXI H,NUM2
EX0:
 MOV B,M
 LDAX D
 MOV M,A
 MOV A,B
 STAX D
 INX H
 INX D
 DCR C
 JNZ EX0
 RET

FDIV:
  PUSH B
  CALL UNPCK
  CALL DIVMAN
  LDA ?EXP2
  MOV B,A
  LDA ?EXP1
  SUB B
;  SUI 2
  STA ?EXP
  LDA ?SIGN2
  MOV B,A
  LDA ?SIGN1
  ADD B
  STA ?SIGN
  CALL NORMAL
  POP B
  CALL PACK
  RET


UNPCK:
  LXI B,NUM1
  XRA A
  STAX B
  INX B
  CALL UNPC
  MOV A,M
  ANI 80H
  STA ?SIGN1
  MOV A,M
  ANI 7FH
  RLC
  RLC
  RAR
  RAR
  STA ?EXP1
  XCHG
  LXI B,NUM2
  XRA A
  STAX B
  INX B
  CALL UNPC
  MOV A,M
  ANI 80H
  STA ?SIGN2
  MOV A,M
  ANI 7FH
  RLC
  RLC
  RAR
  RAR
  STA ?EXP2
  RET

FMUL:
  PUSH B
  CALL UNPACK
  CALL MULMAN
  LDA ?EXP1
  MOV B,A
  LDA ?EXP2
  ADD B
  STA ?EXP
  LDA ?SIGN1
  MOV B,A
  LDA ?SIGN2
  ADD B
  STA ?SIGN
  CALL NORMAL
  POP B
  CALL PACK
  RET

PACK:
  LXI H,RES+1
  MVI E,NUML
  MOV A,M
  STAX B
  INX H
  INX B
  DCR E
  JNZ $-5
  MOV H,B
  MOV L,C
  LDA ?SIGN
  MOV M,A
  LDA ?EXP
  ANI 7FH
  ORA M
  MOV M,A
  RET


UNPACK:
  LXI B,NUM1
  CALL UNPC
  MOV A,M
  ANI 80H
  STA ?SIGN1
  MOV A,M
  ANI 7FH
  RLC
  RLC
  RAR
  RAR
  STA ?EXP1
  XCHG
  LXI B,NUM2
  CALL UNPC
  MOV A,M
  ANI 80H
  STA ?SIGN2
  MOV A,M
  ANI 7FH
  RLC
  RLC
  RAR
  RAR
  STA ?EXP2
  RET
UNPC:
 PUSH D
 PUSH B
 MVI E,NUML
 MOV A,M
 STAX B
 INX B
 INX H
 DCR E
 JNZ $-5
 XRA A
 MVI E,NUML
 STAX B
 INX B
 DCR E
 JNZ $-3
 POP B
 POP D
 RET

?PRLN:
 CALL ?PRNUM
 MVI C,10
 CALL 0F809H
 MVI C,13
 JMP 0F809H

?PRNUM:

 PUSH H
 LXI B,NUMLEN-1
 DAD B
 MVI C,' '
 MOV A,M
 ANI 80H
 POP H
 JZ PRN5
 MVI C,'-'
PRN5:
 CALL 0F809H
 MOV A,M
 RRC
 RRC
 RRC
 RRC
 ANI 0FH
 ADI 30H
 MOV C,A
 CALL 0F809H
 MVI C,'.'
 CALL 0F809H
 MOV A,M
 ANI 0FH
 ADI 30H
 MOV C,A
 CALL 0F809H
 INX H
 MVI E,NUMLEN-2
PRN0:
 MOV A,E
 ORA A
 JZ PRN1
 MOV A,M
 CALL 0F815H
 INX H
 DCR E
 JMP PRN0
PRN1:
 MVI C,20H
 CALL 0F809H
 MVI C,'E'
 CALL 0F809H
 MVI C,'+'
 MOV A,M
 DCR A
 ANI 7FH
 ORA A
 CPI 40H
 JC PRN2
 ANI 3FH
 MVI C,'-'
 MOV B,A
 MVI A,40H
 SUB B
PRN2:
 PUSH PSW
 CALL 0F809H
 POP PSW
 CALL BIN_DC
 JMP 0F815H
BIN_DC:
 MOV B,A
 ORA A
 RZ
 XRA A
?10:
 INR A
 DAA
 DCR B
 JNZ ?10
 RET
DEC_BIN:
 MVI B,0
 ORA A
 RZ
 SUI 99H
 CMA
 INR A
 ADI 1
 DAA
DCB0:
 INR B
 ADI 1
 DAA
 JNZ DCB0
 RET

MULMAN:
 LXI H,RES
 MVI A,NUM1L+NUM2L+1
 MVI M,0
 INX H
 DCR A
 JNZ $-4

 LXI B,RES+NUM1L+NUM2L
 LXI D,NUM1+NUM2L-1
M2:
 LDAX D
 ORA A
 JZ M6
 PUSH B
  XRA A
  STA K
  LXI H,NUM2+NUM2L-1
M4:
  PUSH H
   PUSH B
    LDAX D
    MOV C,A
    MOV B,M
    CALL MULT
   POP B
   LDAX B
   ADD L
   DAA
   MOV L,A
   MOV A,H
   ACI 0
   MOV H,A
   LDA K
   ADD L
   DAA
   STAX B
   MOV A,H
   ACI 0
   DAA
   STA K
   DCX B
  POP H
  DCX H
  MOV A,L
  ANI 0FH
  CPI 15
  JNZ M4
  LDA K
  STAX B
 POP B
M6:
 DCX B
 DCX D
 MOV A,E
 ANI 15
 CPI 15
 JNZ M2
 RET


MULT:
 PUSH D
 PUSH B
 MOV E,C
 MOV A,E
 ANI 0FH
 MOV C,A
 CALL MUL
 MOV A,E
 RRC
 RRC
 RRC
 RRC
 ANI 0FH
 MOV C,A
 XCHG
 CALL MUL
 DAD H
 DAD H
 DAD H
 DAD H
 MOV A,L
 ADD E
 DAA
 MOV L,A
 MOV A,H
 ADC D
 DAA
 MOV H,A
 POP B
 POP D
 RET

MUL:
 LXI H,0
 INR C
MLT0:
 DCR C
 RZ
 MOV A,L
 ADD B
 DAA
 MOV L,A
 MOV A,H
 ACI 0
 DAA
 MOV H,A
 JMP MLT0

K: DS 1
ADDMAN:
 LXI H,NUM1+NUML+NUML-1
 LXI D,NUM2+NUML+NUML-1
 LXI B,RES+NUML+NUML
 XRA A
 MVI A,NUML+NUML
 STA ?TMP
ADDM:
 LDAX D
 ADC M
 DAA
 STAX B
 DCX H
 DCX B
 DCX D
 LDA ?TMP
 DCR A
 STA ?TMP
 JNZ ADDM
 ACI 0
 STAX B
 RET

SUBMAN:
 LXI H,NUM1+NUML+NUML-1
 LXI D,NUM2+NUML+NUML-1
 LXI B,RES+NUML+NUML
 XRA A
 MVI A,NUML+NUML
 STA ?TMP
 STC
SBBM:
 PUSH PSW
 LDAX D
 SUI 99H
 CMA
 INR A
 STAX D
 POP PSW
 LDAX D
 ADC  M
 DAA
 STAX B
 DCX H
 DCX B
 DCX D
 LDA ?TMP
 DCR A
 STA ?TMP
 JNZ SBBM
 MVI A,0
 STAX B
 RET

SUBSM:
 CALL CMPR
 JNC SUBMAN
 LDA ?SIGN
 XRI 80H
 STA ?SIGN
 CALL EXNUM
 JMP SUBMAN

CMPR:
 LXI H,NUM2
 LXI D,NUM1
 MVI B,NUML+1
CMP0:
 LDAX D
 CMP M
 RNZ
 INX H
 INX D
 DCR B
 JNZ CMP0
 RET


?TMP: DS 2
NORMAL:
 LXI H,RES
 MVI C,NUML+NUML+1
N0: MOV A,M
 ORA A
 JNZ N1
 DCR C
 RZ
 INX H
 JMP N0
N1:
 LXI H,RES
 MOV A,M
 ORA A
 JNZ N4
N2: LXI H,RES+1
 MOV A,M
 ANI 0F0H
 JNZ N5
 LXI H,RES+NUML+NUML+1
 MVI C,NUML+NUML+1
 CALL ?NBLLT
 LDA ?EXP
 DCR A
 STA ?EXP
 JMP N2
N4:
 LXI H,RES
 MVI C,NUML+NUML+2
 CALL ?NBLRT
 LDA ?EXP
 INR A
 STA ?EXP
N5:
 LXI H,RES+NUML+1
 MOV A,M
 CPI 50H
 JC N6
N51:
 LXI H,RES+NUML
 MVI C,NUML
 STC
N50:
 MOV A,M
 ACI 0
 DAA
 MOV M,A
 DCX H
 JNC N6
 DCR C
 JNZ N50
 MVI M,1
 JMP N4
N6:
 RET

DIVMAN:
 LDA NUM2+1
 CPI 99H
 JZ D2
 ADI 1
 DAA
 MOV B,A
 LXI H,100H
 CALL DIV
 LXI H,NUM1+NUML+NUML
 PUSH B
 CALL MULONE
 POP B
 LXI H,NUM2+NUML+NUML
 CALL MULONE
D2:
 LXI H,NUM1
 LXI D,RES
 MVI B,NUML+1
D3:
 PUSH H
 PUSH B
 PUSH D
 MOV D,M
 INX H
 MOV E,M
 DCX H
 LDA NUM2+1
 CMP M
 JNZ D30
 MVI C,99H
 JMP D31
D30:
 XCHG
 LDA NUM2+1
 MOV B,A
 CALL DIV
 XCHG

D31:
 PUSH H
 LDA NUM2+1
 MOV B,A
 CALL MULT
 XCHG
 MVI A,99H
 SUB E
 STC
 ADC L
 DAA
 MOV D,A
 POP H
 PUSH H
 INX H
 INX H
 MOV E,M
 LDA NUM2+2
 MOV B,A
 CALL MULT
 MOV A,H
 CMP D
 JC D4
 JZ D4
 MOV A,L
 CMP E
 JC D4
 JZ D4
D32:
 DCR C
 POP H
 JMP D31
D4:POP H
 PUSH B
 PUSH H
 CALL SAVDEL
 LXI H,NUM2+NUM2L+NUM2L
 CALL MULONE
 POP H
 LXI B,NUML
 PUSH H
 DAD B
 MVI B,NUML+1
 LXI D,NUM2+NUML
 STC
D40:
 PUSH PSW
 LDAX D
 MOV C,A
 MVI A,99H
 SUB C
 MOV C,A
 POP PSW
 MOV A,C
 ADC M
 DAA
 MOV M,A
 DCX H
 DCX D
 DCR B
 JNZ D40
 CMC
 PUSH PSW
 CALL LDDEL
 POP PSW
 POP H
 POP B
 POP D
 MOV A,C
 STAX D
 JNC D7
D6:
 DCR A
 STAX D
 PUSH D
 LXI B,NUML
 DAD B
 MVI B,NUML+1
 LXI D,NUM2+NUML
 STC
D60:
 LDAX D
 ADC M
 DAA
 MOV M,A
 DCX H
 DCX D
 DCR B
 JNZ D60
 POP D
D7:
 POP B
 POP H
 INX D
 INX H
 DCR B
 JNZ D3
 RET

SAVDEL:
 PUSH H
 PUSH D
 PUSH B
 LXI H,NUM2
 LXI D,RES1
 JMP MOVE

LDDEL:
 PUSH H
 PUSH D
 PUSH B
 LXI D,NUM2
 LXI H,RES1
 JMP MOVE

MOVE:
 MVI B,NUML+NUML
MOV0:
 MOV A,M
 STAX D
 INX H
 INX D
 DCR B
 JNZ MOV0
 POP B
 POP D
 POP H
 RET

; демеойе 4-и тбътсдопзп BCD-юйумб
; об 2-X тбътсдопе
; демйнпе  -> (HL)
; демйфемш -> (B)
; (C)-> пуфбфпл
; (B)-> юбуфопе

DIV:
 PUSH D
 PUSH H
 PUSH H
 DAD H
 DAD H
 DAD H
 DAD H
 MOV E,H
 POP H
 MOV A,H
 RRC
 RRC
 RRC
 RRC
 ANI 0FH
 MOV D,A
 XCHG
 PUSH B
 CALL DV
 DAD H
 DAD H
 DAD H
 DAD H
 MOV A,E
 ANI 0FH
 ORA L
 MOV L,A
 MOV D,C
 POP B
 CALL DV
 MOV B,L
 MOV A,D
 ADD A
 ADD A
 ADD A
 ADD A
 ORA C
 MOV C,A
 POP H
 POP D
 RET


DV:
 MVI C,0
 MVI A,99H
 SUB B
 INR A
 DAA
 JZ DIVBYZ
 MOV B,A
DV0:
 PUSH H
 MOV A,L
 ADD B
 DAA
 MOV L,A
 MOV A,H
 ACI 99H
 DAA
 MOV H,A
 JNC DV1
 POP PSW
 MOV A,C
 ADI 1
 DAA
 MOV C,A
 JMP DV0
DV1:
 POP H
 RET

MULONE:
 MVI B,NUML+NUML
 MVI D,0
ML0: MOV A,M
 PUSH B
 MOV B,A
 PUSH D
 XCHG
 CALL MULT
 XCHG
 MOV A,E
 MOV B,D
 POP D
 ADD D
 DAA
 MOV M,A
 MVI A,0
 ADC B
 DAA
 MOV D,A
 DCX H
 POP B
 DCR B
 JNZ ML0
 MOV M,D
 RET


; UNSIGNED DECIMAL
; SHIFT LEFT
?NBLLT:
 PUSH H
 PUSH D
 PUSH B
  MVI E,0
 ?32:
   MOV A,M
   RLC
   RLC
   RLC
   RLC
   MOV B,E
   PUSH PSW
   ANI 0FH
   MOV E,A
   POP PSW
   ANI 0F0H
   ORA B
   MOV M,A
   DCX H
   DCR C
  JNZ ?32
 POP B
 POP D
 POP H
 RET

; UNSIGNED DECIMAL
; SHIFT RIGHT

?NBLRT:
 PUSH D
 PUSH B
 MVI E,0
  PUSH H
 ?02:
   MOV A,M
   RLC
   RLC
   RLC
   RLC
   MOV B,E
   PUSH PSW
   ANI 0F0H
   MOV E,A
   POP PSW
   ANI 0FH
   ORA B
   MOV M,A
   INX H
   DCR C
  JNZ ?02
  POP H
 POP B
 POP D
 RET
ERR: DB 'STRONG GLUEK DETESTED',0
DVZ: DB 'DIVISION BY ZERO',0
DIVBYZ:
 LXI H,DVZ
 CALL 0F818H
 JMP 0F86CH


FLOT:
 LXI B,RES1
 CALL RSUB
 MOV H,B
 MOV L,C
 PUSH H
 LXI B,NUML
 DAD B
 MOV A,M
 RRC
 POP H
 RC
 MVI B,NUML
 MOV A,M
 ORA A
 RZ
 INX H
 DCR B
 JNZ $-6
 RET


ERROR:
 LXI H,ERR
 CALL 0F818H
 JMP 0F86CH

?EXP: DS 1
?EXP1:DS 1
?EXP2:DS 1
?SIGN1:DS 1
?SIGN2:DS 1
?SIGN: DS 1
PNT: DS 2
SMB: DS 1
DATA: DS NUML+1
DATA1: DS NUML+1
 END


