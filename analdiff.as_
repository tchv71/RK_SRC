START:
 LXI SP,0E5FFH
 LXI H,8000H
 SHLD FREE
 LXI H,TEXT
 CALL 0F818H
 LXI H,DATA
 CALL INPSTR
 CALL RTREE
  CALL DIFF
 MVI C,10
 CALL 0F809H
 MVI C,13
 CALL 0F809H
 PUSH H
 LXI D,DATA
 CALL PRTREE
 LXI H,DATA
 CALL 0F818H
 POP H
 MVI C,10

 CALL 0F809H
 MVI C,13
 CALL 0F809H
 CALL TIDY
 CALL TIDY
 LXI D,DATA
 CALL PRTREE
 LXI H,DATA
 CALL 0F818H
 CALL 0F803H
 JMP START

?SAME:
 PUSH H
 PUSH D
 PUSH B
 MOV A,H
 ORA A
 JNZ SM0
 MOV A,D
 CMA
 JMP RSM
SM0:
 MOV A,D
 ORA A
 JZ SMFL
 MOV B,D
 MOV C,E
 CALL CMPR
 JNZ SMFL
 PUSH H
 PUSH D
 CALL GETLL
 XCHG
 CALL GETLL
 CALL ?SAME
 POP D
 POP H
 ORA A
 JZ SMFL
 CALL GETRL
 XCHG
 CALL GETRL
 CALL ?SAME
RSM:
 POP B
 POP D
 POP H
 RET
SMFL:
 XRA A
 JMP RSM

TIDY:
 PUSH D
 PUSH B


 CALL ?ATOM
 ORA A
 JNZ RTID2
 MOV D,H
 MOV E,L
 PUSH H
 CALL GETLL
 MOV A,H
 ORA A
 JZ $+6
 CALL TIDY
 MOV B,H
 MOV C,L
 POP H
 CALL GETRL
 MOV A,H
 ORA A
 JZ $+6
 CALL TIDY
 CALL MKT
 PUSH H
 MOV A,M
 PUSH PSW
 PUSH H
 CALL GETRL
 XCHG
 POP H
 CALL GETLL
 POP PSW
 CPI '='
 JZ TIDUN
 CPI '+'
 JZ TIDAD
 CPI '-'
 JZ TIDSB
 CPI '*'
 JZ TIDMUL
 CPI '/'
 JZ TIDDIV
 CPI '^'
 JZ TIDPOW
;CALL 0F803H
 JMP RTID1
RTID:
 POP B
 POP B
 POP D
 RET

RTID1:
 POP H
RTID2:
 POP B
 POP D
 RET

TIDUN:
 XCHG
 LXI B,N0
 CALL CMPR
 JNZ TIDU0
 MVI A,'0'
 CALL MKNS
 JMP RTID
TIDU0:
 MOV A,M
 CPI '='
 INX H
 MOV A,M
 DCX H
 ORA A
 JNZ RTID1
 CALL GETRL
 JMP RTID

TIDAD:
 CALL ?SAME
 ORA A
 JZ TDA0
 LXI B,N0
 CALL CMPR
 JZ RTID
 PUSH H
 MVI A,'2'
 CALL MKNS
 MOV B,H
 MOV C,L
 POP H
 MVI A,'*'
 CALL MKTS
 JMP RTID
TDA0:
 LXI B,N0
 CALL CMPR
 JNZ TIDA0
 XCHG
 JMP RTID

TIDA0:
 XCHG
 LXI B,N0
 CALL CMPR
 JNZ TIDA1
 XCHG
 JMP RTID
TIDA1:
 JMP RTID1

TIDSB:
 CALL ?SAME
 ORA A
 JZ TDS0
 MVI A,'0'
 CALL MKNS
 JMP RTID

TDS0:
 LXI B,N0
 CALL CMPR
 JNZ TDSB0
 XCHG
 LXI B,0
 MVI A,'='
 CALL MKTS
 JMP RTID
TDSB0:
 XCHG
 LXI B,N0
 CALL CMPR
 JNZ RTID1
 XCHG
 JMP RTID

TIDMUL:
 CALL ?SAME
 ORA A
 JZ TDM0
 LXI B,N1
 CALL CMPR
 JZ RTID
 MVI A,'2'
 MOV B,H
 MOV C,L
 CALL MKNS
 MVI A,'^'
 CALL MKTS
 JMP RTID
TDM0:
 LXI B,N1
 CALL CMPR
 JNZ TIDM0
 XCHG
 JMP RTID

TIDM0:
 LXI B,N0
 CALL CMPR
 JNZ TIDM01
 MVI A,'0'
 CALL MKNS
 JMP RTID
TIDM01:
 XCHG
 LXI B,N1
 CALL CMPR
 JNZ TIDM1
 XCHG
 JMP RTID
TIDM1:
 LXI B,N0
 CALL CMPR
 JNZ RTID1
 MVI A,'0'
 CALL MKNS
 JMP RTID

TIDDIV:
 CALL ?SAME
 ORA A
 JZ TDD0
 MVI A,'1'
 CALL MKNS
 JMP RTID
TDD0:
 LXI B,N0
 CALL CMPR
 JNZ TIDD0
 MVI A,'0'
 CALL MKNS
 JMP RTID
TIDD0:
 XCHG
 LXI B,N1
 CALL CMPR
 JNZ RTID1
 XCHG
 JMP RTID


TIDPOW:
 XCHG
 LXI B,N0
 CALL CMPR
 JNZ TIDP0
TP1:
 MVI A,'1'
 CALL MKNS
 JMP RTID
TIDP0:
 XCHG
 LXI B,N0
 CALL CMPR
 JNZ TIDP1
 MVI A,'0'
 CALL MKNS
 JMP RTID
TIDP1:
 LXI B,N1
 CALL CMPR
 JZ TP1
 XCHG
 LXI B,N1
 CALL CMPR
 JNZ RTID1
 XCHG
 JMP RTID

DIFF:
 PUSH D
 PUSH B
 PUSH H
 MOV A,M
 ORA A
 JZ OUTDIF
 CALL ?CONST
 ORA A
 JZ NOCONST
 MVI A,'0'
 CALL MKNS
 JMP OUTD1

NOCONST:
 MOV A,M
 CPI 'X'
 JNZ NOVAR
 MVI A,'1'
 CALL MKNS
 JMP OUTD1

NOVAR:
 MOV A,M
 ORA A
 JZ OUTDIF
 CPI '='
 JZ DIFUN
 CPI '+'
 JZ DIFAD
 CPI '-'
 JZ DIFAD
 CPI '*'
 JZ DIFMLT
 CPI '/'
 JZ DIFDIV
 CPI '^'
 JZ DIFPOW
 CALL SEARCH
 MOV B,H
 MOV C,L
 POP H
 CALL GETRL
 CALL DIFF
 MVI A,'*'
 CALL MKTS
 POP B
 POP D
 RET

SEARCH:
 LXI B,SIN
 CALL CMPR
 JZ DIFSIN
 LXI B,COS
 CALL CMPR
 JZ DIFCOS
 LXI B,TG
 BALL CMPR
 JZ DIFTG
 LXI B,TAN
 CALL CMPR
 JZ DIFTG
 LXI B,LN
 CALL CMPR
 JZ DIFLN
 LXI B,LG
 CALL CMPR
 JZ DIFLG
 LXI B,LOG
 CALL CMPR
 JZ DIFLG
 LXI B,ARCSIN
 CALL CMPR
 JZ DFARSN
 LXI B,ARCCOS
 CALL CMPR
 JZ DFARCS
 LXI B,ARCTAN
 CALL CMPR
 JZ DFATN
 LXI B,ATN
 CALL CMPR
 JZ DFATN
 LXI B,EXP
 CALL CMPR
 JZ DIFEXP
 LXI B,ABS
 CALL CMPR
 JZ DIFABS
 LXI B,SQR
 CALL CMPR
 JZ DIFSQR
 LXI B,SQRT
 CALL CMPR
 JZ DFSQRT
 INX SP
 INX SP
OUTDIF:
 XRA A
 CALL MKNS

OUTD1:
 POP B
 POP B
 POP D
 RET

SIN: DB 'SIN',0
COS: DB 'COS',0
TG:  DB 'TG',0
TAN: DB 'TAN',0
LN:  DB 'LN',0
LG: DB 'LG',0
LOG: DB 'LOG',0
ARCSIN:DB 'ARCSIN',0
ARCCOS:DB 'ARCCOS',0
ARCTAN:DB 'ARCTAN',0
ATN: DB 'ATN',0
EXP: DB 'EXP',0
ABS: DB 'ABS',0
SQR: DB 'SQR',0
SQRT:DB 'SQRT',0
N0:  DB '0',0
N1:  DB '1',0
N10: DB '10',0

DIFUN:
 CALL GETRL
 CALL DIFF
 LXI B,0
 MVI A,'='
 CALL MKTS
 JMP OUTD1

DIFAD:
 MOV D,A
 PUSH H
 CALL GETLL
 CALL DIFF
 MOV B,H
 MOV C,L
 POP H
 CALL GETRL
 CALL DIFF
 MOV A,D
 CALL MKTS
 JMP OUTD1

DIFMLT:
 PUSH H
 CALL GETLL
 CALL DIFF
 MOV B,H
 MOV C,L
 POP H
 PUSH H
 CALL GETRL
 MVI A,'*'
 CALL MKTS
 XCHG
 POP H

 PUSH H
 CALL GETRL
 CALL DIFF
 MOV B,H
 MOV C,L
 POP H
 CALL GETLL
 MVI A,'*'
 CALL MKTS
 MOV B,D
 MOV C,E
 MVI A,'+'
 CALL MKTS
 JMP OUTD1


DIFDIV:
 PUSH H
 PUSH H
 CALL GETLL
 CALL DIFF
 MOV B,H
 MOV C,L
 POP H
 PUSH H
 CALL GETRL
 MVI A,'*'
 CALL MKTS
 XCHG
 POP H

 PUSH H
 CALL GETRL
 CALL DIFF
 MOV B,H
 MOV C,L
 POP H
 CALL GETLL
 MVI A,'*'
 CALL MKTS
 MOV B,D
 MOV C,E
 MVI A,'-'
 CALL MKTS
 MOV B,H
 MOV C,L
 POP H
 CALL GETRL
 PUSH B
 MOV B,H
 MOV C,L
 MVI A,'2'
 CALL MKNS
 MVI A,'^'
 CALL MKTS
 POP B
 MVI A,'/'
 CALL MKTS
 JMP OUTD1


DIFPOW: ; (A^B)'=A'*B*A^(B-1)+A^B*B'*LN(A)
 PUSH H
 CALL GETLL
 LXI B,0
 LXI D,LN;LN(B)
 CALL MKT
 XCHG
 POP H
 PUSH H
 CALL GETRL
 CALL DIFF
 MOV B,H
 MOV C,L
 XCHG
 MVI A,'*'; B'*LN(A)
 CALL MKTS
 POP B

 MVI A,'*'; A^B*B'*LN(A)
 CALL MKTS
 PUSH H

 MOV H,B
 MOV L,C
 PUSH H
 CALL GETRL
 MOV B,H
 MOV C,L
 MVI A,'1'
 CALL MKNS
 MVI A,'-'
 CALL MKTS  ; B-1
 XCHG
 POP H
 PUSH H
 CALL GETLL
 MOV B,H
 MOV C,L
 XCHG
 MVI A,'^'
 CALL MKTS ; A^(B-1)
 XCHG
 POP H
 PUSH H
 CALL GETRL
 MOV B,H
 MOV C,L
 XCHG
 MVI A,'*'
 CALL MKTS ; B*A^(B-1)
 XCHG
 POP H
 CALL GETLL
 CALL DIFF
 MOV B,H
 MOV C,L
 XCHG
 MVI A,'*'
 CALL MKTS ; A'*B*A^(B-1)
 MOV B,H
 MOV C,L
 POP H
 MVI A,'+'
 CALL MKTS
 JMP OUTD1


DIFSIN:
 CALL GETRL
 LXI D,COS
 LXI B,0
 CALL MKT
 RET

DIFCOS:
 CALL GETRL
 LXI B,0
 LXI D,SIN
 CALL MKT
 LXI B,0
 MVI A,'='
 CALL MKTS
 RET

DIFTG:
 CALL GETRL
 LXI D,COS
 LXI B,0
 CALL MKT
 MOV B,H
 MOV C,L
 MVI A,'2'
 CALL MKNS
 MVI A,'^'
 CALL MKTS
 PUSH H
 MVI A,'1'
 CALL MKNS
 MOV B,H
 MOV C,L
 POP H
 MVI A,'/'
 CALL MKTS
 RET

DIFLN:
 CALL GETRL
 PUSH H
 MVI A,'1'
 CALL MKNS
 MOV B,H
 MOV C,L
 POP H
 MVI A,'/'
 CALL MKTS
 RET

DIFLG:
 CALL DIFLN
 MOV B,H
 MOV C,L
 PUSH B
 LXI D,N10
 CALL MKN
 LXI B,0
 LXI D,LN
 CALL MKT
 POP B
 MVI A,'/'
 CALL MKTS
 RET

DFARSN:
 CALL GETRL
 MOV B,H
 MOV C,L
 MVI A,'2'
 CALL MKNS
 MVI A,'^'
 CALL MKTS
 PUSH H
 MVI A,'1'
 CALL MKNS
 MOV B,H
 MOV C,L
 POP H
 MVI A,'-'
 CALL MKTS
 LXI D,SQRT
 LXI B,0
 CALL MKT
 PUSH H
 MVI A,'1'
 CALL MKNS
 MOV B,H
 MOV C,L
 POP H
 MVI A,'/'
 CALL MKTS
 RET

DFARCS:
 CALL DFARSN
 MVI A,'='
 LXI B,0
 CALL MKTS
 RET

DFATN:
 CALL GETRL
 MOV B,H
 MOV C,L
 MVI A,'2'
 CALL MKNS
 MVI A,'^'
 CALL MKTS
 PUSH H
 MVI A,'1'
 CALL MKNS
 MOV B,H
 MOV C,L
 POP H
 MVI A,'+'
 CALL MKTS
 PUSH H
 MVI A,'1'
 CALL MKNS
 MOV B,H
 MOV C,L
 POP H
 MVI A,'/'
 CALL MKTS
 RET

DIFEXP:
 RET

DIFABS:
 MOV B,H
 MOV C,L
 CALL GETRL
 MVI A,'/'
 CALL MKTS
 RET

DIFSQR:
 CALL GETRL
 PUSH H
 MVI A,'2'
 CALL MKNS
 MOV B,H
 MOV C,L
 POP H
 MVI A,'*'
 CALL MKTS
 RET

DFSQRT:
 PUSH H
 MVI A,'2'
 CALL MKNS
 MOV B,H
 MOV C,L
 POP H
 MVI A,'*'
 CALL MKTS
 PUSH H
 MVI A,'1'
 CALL MKNS
 MOV B,H
 MOV C,L
 POP H
 MVI A,'/'
 CALL MKTS
 RET


CMPR:
 PUSH H
CMP0: LDAX B
 ORA A
 JNZ CMP1
 MOV A,M
 ORA A
 JNZ NCRET
 JMP CRET
CMP1:
 CMP M
 JNZ NCRET
 INX B
 MOV A,M
 ORA A
 INX H
 JNZ CMP0
NCRET:
 MVI A,0FFH
 ORA A
CRET:
 POP H
 RET


?CONST:
 PUSH H
 MOV A,M
 CPI '0'
 JC ?NC
 CPI '9'+1
 JNC ?NC
 CALL RDFIG
 MOV A,M
 CPI '.'
 JNZ $+8
 INX H
 CALL RDFIG
 MOV A,M
 CPI 'E'
 JNZ ?CNST2
 INX H
 MOV A,M
 CPI '+'
 JZ ?CNST0
 CPI '-'
 JNZ ?CNST1
?CNST0:
 INX H
?CNST1:
 MOV A,M
 CPI '0'
 JC ?NC
 CPI '9'+1
 JC ?NC
 CALL RDFIG
?CNST2:
 MOV A,M
 ORA A
 JNZ ?NC
 POP H
 MVI A,0FFH
 RET

?NC:
 XRA A
 POP H
 RET

RDFIG:
 MOV A,M
 CPI 30H
 RC
 CPI 3AH
 RNC
 INX H
 JMP RDFIG

RTREE:
 SHLD PNT
 CALL READ
 JMP EXPR

READ:
 PUSH H
 LHLD PNT
 MOV A,M
 INX H
 SHLD PNT
 POP H
 STA SMB
 RET

FIG:
 LDA SMB
 CPI 30H
 RC
 CPI 3AH
 RNC
 STAX D
 INX D
 CALL READ
 JMP FIG

MKN:
 PUSH D
 LHLD FREE
 PUSH H
MKN0: LDAX D
 MOV M,A
 INX H
 INX D
 ORA A
 JNZ MKN0

 MVI A,4
 MVI M,0
 INX H
 DCR A
 JNZ $-4
 SHLD FREE
 POP H
 POP D
 RET

MKT:
 PUSH D
 SHLD TMP
 CALL MKN
 PUSH H
 MOV A,M
 ORA A
 INX H
 JNZ $-3

 XCHG
 LHLD TMP
 XCHG
 MOV M,E
 INX H
 MOV M,D
 INX H
 MOV M,C
 INX H
 MOV M,B
 POP H
 POP D
 RET

MKNS:
 LHLD FREE
 PUSH H
 MOV M,A
 INX H
 MVI M,0
 INX H

 MVI A,4
 MVI M,0
 INX H
 DCR A
 JNZ $-4
 SHLD FREE
 POP H
 RET

MKTS:
 PUSH D
 SHLD TMP
 CALL MKNS
 PUSH H
 MOV A,M
 ORA A
 INX H
 JNZ $-3

 XCHG
 LHLD TMP
 XCHG
 MOV M,E
 INX H
 MOV M,D
 INX H
 MOV M,C
 INX H
 MOV M,B
 POP H
 POP D
 RET

TMP: DS 2

NUM:
 PUSH D
 LXI D,SRLNUM
 LDA SMB
 CPI 30H
 JC ERROR
 CPI 3AH
 JNC ERROR
 CALL FIG
 LDA SMB
 CPI '.'
 JNZ $+8
 STAX D
 INX D
 CALL READ
 CALL FIG
 LDA SMB
 CPI 'E'
 JNZ NUM2
 STAX D
 INX D
 CALL READ
 LDA SMB
 CPI '+'
 JZ NUM0
 CPI '-'
 JNZ NUM1
NUM0:
 STAX D
 INX D
 CALL READ
NUM1:
 LDA SMB
 CPI 30H
 JC ERROR
 CPI 3AH
 JNC ERROR
 CALL FIG
NUM2:
 XRA A
 STAX D
 LXI D,SRLNUM
 CALL MKN
 POP D
 RET

SRLNUM: DS 20

FUNC:
 PUSH D
 PUSH B
 LXI B,SFUNC
 MVI A,4
PUSHSTR:
 PUSH PSW
 LDAX B
 MOV E,A
 INX B
 LDAX B
 MOV D,A
 INX B
 POP PSW
 PUSH D
 DCR A
 JNZ PUSHSTR
 PUSH B
 LXI D,SFUNC
FUN0:
 LDA SMB
 CPI 'A'
 JC FUN1
 CPI 'Z'+1
 JNC FUN1
 STAX D
 INX D
 CALL READ
 JMP FUN0
FUN1:
 XRA A
 STAX D
 LDA SMB
 CPI '('
 JNZ ERROR
 CALL READ
 CALL EXPR
 LDA SMB
 CPI ')'
 JNZ ERROR
 CALL READ
 LXI B,0
 LXI D,SFUNC
 CALL MKT
 POP B
 DCX B
 MVI A,4
POPSTR:
 POP D
 PUSH PSW
 MOV A,D
 STAX B
 DCX B
 MOV A,E
 STAX B
 DCX B
 POP PSW
 DCR A
 JNZ POPSTR

 POP B
 POP D
 RET

SFUNC: DS 20
MLT:
 PUSH D
 PUSH B
 LDA SMB
 CPI '+'
 JZ MLT0
 CPI '-'
 JNZ MLT3
MLT0:
 MOV B,A
 CALL READ
 CMP B
 JZ ERROR
 MOV A,B
 CPI '-'
 JNZ MLT3
 LXI B,0
 CALL MLTN
 MVI A,'='
 CALL MKTS
 JMP MLT2
MLT3:
 CALL MLTN
MLT2:
 POP B
 POP D
 RET

MLTN:
 LDA SMB
 CPI 'X'
 JNZ MLTN10
 CALL MKNS
 CALL READ
 RET

MLTN10:
 CPI 'A'
 JC MLTN0
 CPI 'Z'+1
 JNC MLTN0
 CALL FUNC
 RET
MLTN0:
 CPI '('
 JZ MLTN1
 CALL NUM
 RET
MLTN1:
 CALL READ
 CALL EXPR
 LDA SMB
 CPI ')'
 JNZ ERROR
 CALL READ
 RET

POW:
 PUSH B
 CALL MLT
POW0: LDA SMB
 CPI '^'
 JNZ POW2
POW1:PUSH PSW
 CALL READ
 MOV B,H
 MOV C,L
 CALL MLT
 POP PSW
 CALL MKTS
 JMP POW0
POW2:
 POP B
 RET

ADR:
 PUSH B
 CALL POW
ADR0: LDA SMB
 CPI '*'
 JZ ADR1
 CPI '/'
 JNZ ADR2
ADR1: PUSH PSW
 CALL READ
 MOV B,H
 MOV C,L
 CALL POW
 POP PSW
 CALL MKTS
 JMP ADR0
ADR2:
 POP B
 RET

EXPR:
 PUSH B
 CALL ADR
EXPR0: LDA SMB
 CPI '+'
 JZ EXPR1
 CPI '-'
 JNZ EXPR2
EXPR1: PUSH PSW
 CALL READ
 MOV B,H
 MOV C,L
 CALL ADR
 POP PSW
 CALL MKTS
 JMP EXPR0
EXPR2:
 POP B
 RET


ERROR:
 LXI H,ERRT
 CALL 0F818H
 JMP 0F86CH


?ERR: DS 1


TEXT: DB 1FH,10,13,'במבליפי‏וףכןו היזזועומדיעןקבמיו'
      DB 10,13,'        (C) BITBOYS 1988'
      DB 10,13,'Y(X)=',0

ERRT: DB 10,13,'GTRONG GLUEK DETESTED',10,13,0
SMB: DS 1
PNT1: DS 2

; נו‏בפר גימבעמןחן הועוקב
PRTREE:

PUSH H
PUSH B
 MOV A,M
 CPI 0
 JNZ NO_EMP
 XRA A
 STAX D
 JMP RTRN

NO_EMP:
 CALL ?ATOM
 ORA A
 JZ NO_AT
 CALL CPFLD
 JMP RTRN

NO_AT:
 MOV A,M
 CPI '='
 JNZ NO_MIN
 CALL GETRL
 CALL ?ATOM
 ORA A
 JNZ UNATM
 CALL ?FUNC
 ORA A
 JNZ UNATM
 MVI A,'-'
 STAX D
 INX D
 MVI A,'('
 STAX D
 INX D
 CALL PRTREE
 MVI A,')'
 STAX D
 INX D
 XRA A
 STAX D
 JMP RTRN

UNATM: MVI A,'-'
 STAX D
 INX D
 CALL PRTREE
 JMP RTRN

NO_MIN:
 CALL ?FUNC
 ORA A
 JZ NO_FUN
 CALL CPFLD
 MVI A,'('
 STAX D
 INX D
 CALL GETRL
 CALL PRTREE
 MVI A,')'
 STAX D
 INX D
 XRA A
 STAX D
 JMP RTRN

NO_FUN:
 PUSH H
 CALL GETLL
 CALL ?ATOM
 MOV B,H
 MOV C,L
 POP H
 PUSH H
 ORA A
 JNZ NOPRL
  LDAX B
  CALL ?PRIOR
  MOV H,B
  MOV L,C
  ORA A
  JZ NO_PRL
   MVI A,'('
   STAX D
   INX D
   CALL PRTREE
   MVI A,')'
   STAX D
   INX D
   XRA A
   STAX D
   JMP LATM
 NOPRL:
  MOV H,B
  MOV L,C
 NO_PRL:
   CALL PRTREE
LATM:
 POP H
 PUSH H

 CALL CPFLD
 CALL GETRL
 CALL ?ATOM
 MOV B,H
 MOV C,L
 ORA A
 POP H
 JZ NORATM
 MOV H,B
 MOV L,C
 CALL PRTREE
 JMP RTRN

 NORATM:
  LDAX B
  CALL ?PRIOR
  MOV H,B
  MOV L,C
  ORA A
  JZ NO_PR
   MVI A,'('
   STAX D
   INX D
   CALL PRTREE
   MVI A,')'
   STAX D
   INX D
   XRA A
   STAX D
   JMP RTRN
 NO_PR:
   CALL PRTREE
RTRN:
POP B
POP H
RET

CPFLD:
 PUSH H
CPFL0: MOV A,M
 STAX D
 INX H
 INX D
 ORA A
 JNZ CPFL0
 DCX D
 POP H
 RET

?ATOM:
 MOV A,H
 ORA A
 JZ AT
 PUSH H
 CALL GETRL
 MOV A,H
 ORA A
 POP H
 JNZ NOAT
 PUSH H
 CALL GETLL
 MOV A,H
 ORA A
 JNZ NOAT
 POP H
AT:
 MVI A,0FFH
 RET

NOAT:
 XRA A
 RET

GETLL:
 PUSH D
GETL0:
 MOV A,M
 ORA A
 INX H
 JNZ GETL0
 INX H
 INX H
 MOV E,M
 INX H
 MOV D,M
 XCHG
 POP D
 RET

GETRL:
 PUSH D
GETR0:
 MOV A,M
 ORA A
 INX H
 JNZ GETR0
 MOV E,M
 INX H
 MOV D,M
 XCHG
 POP D
 RET

?FUNC:
 PUSH H
 MOV A,M
 ORA A
 JZ FNFL
?FN0:
 MOV A,M
 ORA A
 JZ FNTR
 CPI 'A'
 JC FNFL
 CPI 'Z'+1
 JNC FNFL
 INX H
 JMP ?FN0

FNFL:
 XRA A
 POP H
 RET

FNTR:
 MVI A,0FFH
 POP H
 RET

?PRIOR:
 CPI '+'
 JZ LOWPR
 CPI '-'
 JZ LOWPR
 CPI '*'
 JZ MDLPR
 CPI '/'
 JZ MDLPR
NPR:
 XRA A
 RET
LOWPR:
 MOV A,M
 CPI '-'
 JZ PR
 CPI '*'
 JZ PR
MDLPR:
 MOV A,M
 CPI '/'
 JZ PR
 CPI '^'
 JNZ NPR
PR:
 MVI A,0FFH
 RET

INPSTR:
 PUSH H
 MVI B,1
INP0:
 CALL 0F803H
 CPI 7FH
 JNZ SYM
 DCR B
 JNZ SYMLT
 MVI B,1
 JMP INP0
SYMLT:
 DCX H
 PUSH H
 LXI H,CURLT
 CALL 0F818H
 POP H
 JMP INP0
CURLT:
 DB 8,20H,8,0
SYM:
 CPI 0DH
 JZ _CR
 CPI 3
 JZ 0F86CH
 CPI 20H
 JC INP0

 MOV M,A
 MOV C,A
 CALL 0F809H
 INR B
 INX H
 JMP INP0
_CR:
 MVI M,0
 POP H
 RET

PNT: DS 2
FREE: DS 2
DATA: EQU $







