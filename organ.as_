;ORG 0
;3-X ηομοσωκ όμελτςοοςηαξ
;
PSG: EQU 0F760H
PPI: EQU 0F780H
PSGV:EQU 0F740H
LOTMP: EQU 0F000H
VRAM: EQU 0D0F2H
PNT: EQU '*'
πδπ: EQU 0F7E0H
DIS: EQU 0F7C0H

 CALL CLS
 LXI H,DIS+1
 MVI M,0
 DCX H
 MVI M,4DH
 MVI M,26H
 MVI M,77H
 MVI M,13H
 INX H
 MVI M,27H
 MOV A,M
 MOV A,M
 ANI 20H
 JZ $-3
 LXI H,πδπ+8
 MVI M,80H
 LXI H,πδπ+4
 MVI M,0
 MVI M,0D0H
 INX H
 MVI M,0E1H
 MVI M,4BH
 LXI H,πδπ+8
 MVI M,0A4H

 CALL PRINT
 DW VRAM+128
 DB 'VERSION  1.0',80H
 CALL PRINT
 DW VRAM+207
 DB '8-OCT-1988',80H
 CALL PRINT
 DW VRAM+355
 DB '*  *** *** *  * *',80H
 CALL PRINT
 DW VRAM+432
 DB '* * * * *  * * ***',80H
 CALL PRINT
 DW VRAM+510
 DB '* * *** *  *** * *',80H
 CALL PRINT
 DW VRAM+589
 DB '*  *   *  * * * *',80H
 CALL PRINT
 DW VRAM+744
 DB 'FOR SPECIAL 3-CANNEL',80H
 CALL PRINT
 DW VRAM+901
 DB 'PROGRAMMING SOUND',80H
 CALL PRINT
 DW VRAM+1058
 DB 'GENERATOR ONLY',80H
 CALL PRINT
 DW VRAM+1213
 DB '(C) BITBOYS 1988',80H

 LXI H,VRAM+1326
 MVI A,64
 MVI M,''
 DCR A
 INX H
 JNZ $-4
 MVI A,3
 LXI H,OLDSND
 MVI M,0
 DCR A
 INX H
 JNZ $-4

 MVI A,30
 STA LOUDN
 CALL DRFORM
 CALL DISLDN
 MVI A,24
M30:
 PUSH PSW
 CALL DISKEY
 POP PSW
 INR A
 CPI 63
 JNZ M30
 MVI A,8
M31:
 DCR A
 PUSH PSW
 CALL DISNK
 POP PSW
 ORA A
 JNZ M31
 CALL INITPL
M00: CALL LOOKBD
 MVI A,0FDH
 STA PPI
 LDA PPI+1
 ANI 02H
 CZ CHGFRM
 MVI A,0FDH
 STA PPI
 LDA PPI+1
 ANI 01H
 CZ LDN
 MVI A,0FDH
 STA PPI
 LDA PPI+1
 ANI 04H
 CZ CHGREP
 MVI A,7FH
 STA PPI
 LDA PPI+1
 ANI 80H
 CZ NOISE
 LDA LOUDN
 MOV B,A
M01: MVI A,080H
 DCR A
 JNZ $-1
 DCR B
 JNZ M01
 JMP M00


CLS:
 LXI H,0D000H
 LXI B,0BE2H
CLS0:MVI M,0
 INX H
 DCX B
 MOV A,B
 ORA C
 JNZ CLS0
 RET

CHGFRM:
 CALL DISLDN
 CALL DRFORM
 MVI H,0
 MVI M,PNT
 LXI D,FORM
 MVI B,0
KEYIN:
 CALL 0F81BH
 CPI 8
 JZ LFT
 CPI 18H
 JZ RHT
 CPI 19H
 JZ UP
 CPI 1AH
 JZ DWN
 CPI 1BH
 JZ INITPL
 CPI 1
 JZ DEL
 CPI 2
 JZ INS
 CALL DRSTR
 JMP KEYIN

DRSTR:
 LDAX D
 MVI L,31
 CPI 30
 JNC KEY2
 SUI 30
 CMA
 INR A
 PUSH H
KEY3: PUSH PSW
 CALL PRESET
 POP PSW
 DCR L
 DCR A
 JNZ KEY3
 POP H
KEY2:
 CALL PRESET
 CALL DELAY
 LDAX D
 MVI L,31
 CPI 30
 JNC KEY0
 SUI 30
 CMA
 INR A
 PUSH H
KEY1: PUSH PSW
 CALL PSET
 POP PSW
 DCR L
 DCR A
 JNZ KEY1
 CALL PRESET
 POP H
KEY0:
 CALL PSET
 JMP DELAY

DELAY:
 PUSH B
 LXI B,1000
 DCX B
 MOV A,B
 ORA C
 JNZ $-3
 POP B
 RET

LDN:
 CALL 0F81BH
 CPI 8
 JZ LDNDN
 CPI 18H
 JZ LDNUP
 CPI 1BH
 JZ INITPL
 CALL DISLDN
 JMP LDN

LDNUP:
 LDA LOUDN
 INR A
 CPI 64
 JZ LDN
 STA LOUDN
 CALL DISLDN
 JMP LDN

LDNDN:
 LDA LOUDN
 DCR A
 JZ LDN
 STA LOUDN
 CALL DISLDN
 JMP LDN

DISLDN:
 PUSH H
 LXI H,VRAM
 LDA LOUDN
 MVI M,0
 INX H
 DCR A
 JNZ $-4
 POP H
 CALL DELAY
 PUSH H
 LXI H,VRAM
 LDA LOUDN
 MVI M,'*'
 INX H
 DCR A
 JNZ $-4
 MVI M,0
 POP H
 CALL DELAY
 RET

LFT:
 MOV A,B
 ORA A
 JZ KEYIN
 DCR B
 DCR H
 DCX D
 CALL DRSTR
 JMP KEYIN
RHT:
 INX D
 LDAX D
 DCX D
 ORA A
 JZ KEYIN
 INX D
 INR H
 INR B
 CALL DRSTR
 JMP KEYIN
UP:
 LDAX D
 DCR A
 CPI 1
 JZ KEYIN
 STAX D
 CALL DRSTR
 JMP KEYIN
DWN:
 LDAX D
 INR A
 JZ KEYIN
 STAX D
 CALL DRSTR
 JMP KEYIN

INS:
 PUSH H
 PUSH B
 XCHG
 MVI B,0
 MOV A,M
 INX H
 INR B
 ORA A
 JNZ $-5
 NOP
 NOP
 NOP
 MOV A,M
 INX H
 MOV M,A
 DCX H
 DCX H
 DCR B
 JNZ $-8
 XCHG
 POP B
 POP H
 CALL CLR
 CALL DRFORM
 JMP KEYIN

DEL:
 PUSH D
 PUSH H
 PUSH B
 XCHG
DEL1: INX H
 MOV A,M
 ORA A
 JZ DEL0
 DCX H
 MOV M,A
 INX H
 JMP DEL1
DEL0: DCX H
 MOV M,A
 INX H
 INX H
 MOV A,M
 DCX H
 MOV M,A
 XCHG
 CALL CLR
 CALL DRFORM
 POP B
 POP H
 POP D
 JMP KEYIN

CLR:
 PUSH H
 PUSH D
 PUSH B
 LXI H,VRAM+78
 MVI D,15
CLR0: MVI B,32
 MVI M,0
 INX H
 DCR B
 JNZ $-4
 LXI B,78-32
 DAD B
 DCR D
 JNZ CLR0
 POP B
 POP D
 POP H
 RET

CHGREP:
 MVI H,0
 MVI L,33
 LXI D,FORM
 LDAX D
 INX D
 ORA A
 JNZ $-3
 LDAX D
 MOV H,A
 DCR H

KINR:
 CALL 0F803H
 CPI 8
 JZ RLFT
 CPI 18H
 JZ RRHT
 CPI 1BH
 JZ  INITPL
 JMP KINR

RLFT:
 LDAX D
 DCR A
 JZ KINR
 STAX D
 CALL PRESET
 DCR H
 CALL PSET
 JMP KINR

RRHT:
 LDAX D
 INR A
 STAX D
 CALL PRESET
 INR H
 CALL PSET
 JMP KINR

DRFORM:
 PUSH H
 PUSH D
 PUSH B
 LXI D,FORM
 MVI H,0
DRF1:
 MVI L,31
 LDAX D
 ORA A
 JZ DRF0
 CPI 30
 JNC DRF2
 SUI 30
 CMA
 INR A
 PUSH H
DRF6: PUSH PSW
 CALL PSET
 POP PSW
 DCR L
 DCR A
 JNZ DRF6
 CALL PRESET
 POP H
DRF2:
 CALL PSET
 INR H
 INX D
 JMP DRF1
DRF0:
 INX D
 LDAX D
 MOV H,A
 DCR H
 MVI L,33
 CALL PSET
 POP B
 POP D
 POP H
 RET

INITPL:
 LXI H,PSG+3
 MVI M,36H
 MVI M,76H
 MVI M,0B6H
 LXI H,PPI+2
 MVI M,6
 LXI H,0F740H+3
 MVI M,14H
 MVI M,54H
 MVI M,94H
 DCX H
 MVI M,2
 DCX H
 MVI M,2
 DCX H
 MVI M,2
 RET


PLAY:; B - ξονες ξοτω, α - ξονες λαξαμα
 PUSH B
 MOV C,A
 MOV A,B
 ORA A
 MOV A,C
 POP B
 JZ PAUSE
 CALL CHGNOT
 PUSH PSW
 MOV A,B
 MVI C,0FFH
PL0:
 INR C
 SUI 12
 JNC PL0
 ADI 13
 MOV B,A
 POP PSW
 JMP SOUND

CHGNOT:
 PUSH PSW
 MOV A,B
 CPI 36
 JC OB
 CPI 48
 JC O1
 LDA PPI+2
 ANI 80H
 JNZ CEND
 MOV A,B
 ADI 24
 MOV B,A
CEND:
 POP PSW
 RET

OB:
 LDA PPI+2
 ANI 20H
 JNZ CEND
 MOV A,B
 SUI 12
 MOV B,A
 JMP CEND

O1:
 LDA PPI+2
 ANI 40H
 JNZ CEND
 MOV A,B
 ADI 24
 MOV B,A
 JMP CEND

SOUND:; B - ξονες ξοτω, C - ξOνες ολταχω,A - ξονες λαξαμα
 PUSH H
 PUSH D
 PUSH PSW
 LXI H,PLTBL
 MOV A,B
 DCR A
 ADD A
 MOV E,A
 MVI D,0
 DAD D
 MOV E,M
 INX H
 MOV D,M
 XCHG
 MOV B,C
 CALL ?SRNHB
 XCHG
 LXI H,PSG
 POP PSW
 MOV C,A
 MVI B,0
 DAD B
 MOV M,E
 MOV M,D
 POP D
 POP H
 RET

PAUSE:
 PUSH PSW
 RRC
 RRC
 ORI 36H
 STA PSG+3
 POP PSW
 RET

NOISE:
 MVI A,14H
 STA PSGV+3
 LXI H,PSG+3
 MVI M,54H
 MVI M,36H
 MVI A,2
 STA PSGV
 STA PSGV+2
 MVI A,6
 STA PPI+2
 MVI A,8
 STA PSG+1
 MVI B,1
 LXI H,FORM

NOISE0:
 DCR B
 JNZ NOISE1
 LDA LOUDN
 MOV B,A
 MOV A,M
 ORA A
 CZ ENDF
 STA PSGV
 INX H
NOISE1:
 LXI D,36
NOISE2:
 LDA PSG+1
 ADI 2
 STA PSG
 DCX D
 MOV A,D
 ORA E
 JNZ NOISE2
 LDA PPI+1
 ANI 80H
 JZ NOISE0
 LXI H,PSG+3
 MVI M,36H
 MVI M,76H
 RET

ENDF:
 INX H
 MOV E,M
 DCR E
 MVI D,0
 LXI H,FORM
 DAD D
 MOV A,M
 RET

; 16-BIT MULTIPLY
; HL *= DE
?MULHD:
 MOV B,H
 MOV C,L
 LXI H,0
 MVI A,16
?01:
 DAD H
 XCHG
 DAD H
 XCHG
 JNC ?02
 DAD B
?02:
 DCR A
 JNZ ?01
 RET


?ABSHL:
 MOV A,H
 ORA A
 RP
?NEGHL:
 DCX H
?NOTHL:
 MOV A,L
 CMA
 MOV L,A
 MOV A,H
 CMA
 MOV H,A
 RET

; 16-BIT UNSIGNED SHIFT RIGHT
; HL >>= B
?SRNHB:
 INR B
?71:
 DCR B
 RZ
 ANA A
 MOV A,H
 RAR
 MOV H,A
 MOV A,L
 RAR
 MOV L,A
 JMP ?71

; λοόζζιγιεξτω δεμεξιρ δμρ ξοτ λοξτςολταχω
; 16.5νηγ;16 νηγ
PLTBL:
 DW 0DB34H;0D459H - δο
 DW 0CEE7H;0C86EH - δο#
 DW 0C34AH;0BD2EH - ςε
 DW 0B854H;0B290H - ςε#
 DW 0ADFCH;0A88AH - νι
 DW 0A438H;09F15H - ζα
 DW 09B00H;09627H - ζα#
 DW 0924DH;08DBAH - σομψ
 DW 08A17H;085C5H - σομψ#
 DW 08257H;07E43H - μρ
 DW 07B00H;0772DH - μρ#
 DW 0741FH;0707DH - σι


LOOKBD:
 CALL CLRTBL
 CALL CONIN
 JMP SETPSG

CLRTBL:
 LXI H,WRKARE
 MVI A,ENDWRK-WRKARE
 MVI M,0
 INX H
 DCR A
 JNZ $-4
 LXI H,NEWSND
 SHLD SNDPNT
 RET


CONIN:
 MVI B,0FBH
 MVI C,6
 MVI E,30H
 LXI H,CHR
NORM0:
 MOV A,B
 STA PPI
 LDA PPI+1
 CPI 0FFH
 CNZ PRESS
 MOV A,B
 RLC
 MOV B,A
 MOV A,E
 ADI 8
 MOV E,A
 DCR C
 JNZ NORM0
 RET

PRESS:
 PUSH B
 PUSH D
 MVI D,8
PRES0:
 RRC
 CNC FOUND
 INR E
 DCR D
 JNZ PRES0
PRES02:
 POP D
 POP B
 RET

FOUND:
 MOV C,A
 PUSH H
 PUSH D
 PUSH B
 MOV A,E
 SUI 30H
 MOV C,A
 MVI B,0
 LXI H,KEYBRD
 DAD B
 MOV A,M
 LHLD SNDPNT
 MOV M,A
 INX H
 SHLD SNDPNT
EX:
 POP B
 POP D
 POP H
 MOV A,C
 RET

CHGFORM:
 PUSH H
 PUSH D
 PUSH B
 PUSH PSW
 PUSH PSW
 LXI D,NUMFRM
 LXI H,NOTES
 INR A
CHG1: DCR A
 JZ CHG0
 INX H
 INX D
 JMP CHG1
CHG0: MOV A,M
 CMP B
 JZ OLD
 MOV M,B
 XCHG
 MVI M,1
 JMP VOL
OLD:
 XCHG
INR M
VOL:
 MOV C,M
 DCR C
 MVI B,0
 XCHG
 LXI H,FORM
 MVI B,0
 DAD B
 MOV A,M
 CPI 0
 JNZ VOL1
 INX H
 MOV A,M
 XCHG
 MOV M,A
 JMP VOL
VOL1:
 MOV D,A
 POP PSW
 MOV C,A
 MVI B,0
 LXI H,0F740H
 DAD B
 MOV M,D
 POP PSW
 POP B
 POP D
 POP H
 RET


SETPSG:
 CALL SORTBL
 CALL SETFRQ
 JMP  SETVOL


SORTBL: LXI H,NEWSND
 LXI D,OLDSND
 LXI B,0

SRHLP: LDAX D
 CMP M
 JZ FNDNT
 INX D
 INR B
 MOV A,B
 CPI 3
 JNZ SRHLP
PLNT: MOV A,C
 JMP NXTNT

FNDNT:
 ORA A
 JZ PLNT
 PUSH D
 PUSH B
 PUSH H
 LXI H,NEWSND
 MVI D,0
 MOV E,B
 DAD D
 MOV C,M
 XCHG
 POP H
 MOV A,M
 STAX D
 MOV M,C
 POP B
 POP D

NXTNT:
 INR C
 INX H
 LXI D,OLDSND
 MVI B,0
 MOV A,C
 CPI 3
 JNZ SRHLP
 RET


SETFRQ:
 LXI H,NEWSND+2
 MVI A,3
SETFR0:
 DCR A
 PUSH PSW
 MOV B,M
 CALL PLAY
 POP PSW
 DCX H
 ORA A
 JNZ SETFR0
 RET


SETVOL:
 LXI D,NEWSND
 LXI H,OLDSND
 LXI B,VOLSND
 MVI A,3
 STA XXX
M12: LDAX D
 ORA A
 JZ M10
 CMP M
 JZ M11
M10: MVI A,0
 STAX B
M11:
 LDAX B
 INR A
 STAX B
 CALL SETONC
 INX B
 INX H
 INX D
 LDA XXX
 DCR A
 STA XXX
 JNZ M12

 LXI H,OLDSND
 MVI B,3
DSK0:
 MOV A,M
 ORA A
 JZ DCK2
 PUSH H
 PUSH B
 CALL DISKEY
 POP B
 POP H
DCK2:
 INX H
 DCR B
 JNZ DSK0

 LXI H,NEWSND
 MVI B,3
DSK1:
 MOV A,M
 CPI 0
 JZ DSK3
 PUSH H
 PUSH B
 CALL DISPK
 POP B
 POP H
DSK3:
 INX H
 DCR B
 JNZ DSK1

 LDA PPI+2
 ANI 20H
 JZ DSO0
 CALL PRINT
 DW VRAM+2509+78
 DB 'βομψϋαρ ολταχα',80H
 JMP DSO1
DSO0:
 CALL PRINT
 DW VRAM+2509+78
 DB ' ναμαρ  ολταχα',80H
DSO1:
 LDA PPI+2
 ANI 40H
 JZ  DSO2
 CALL PRINT
 DW VRAM+2534+78
 DB '1-ρ ολταχα',80H
 CALL PRINT
 DW VRAM+1332+78
 DB '1-ρ ολταχα',80H
 JMP DSO3
DSO2:
 CALL PRINT
 DW VRAM+2534+78
 DB '3-ρ ολταχα',80H
 CALL PRINT
 DW VRAM+1332+78
 DB '3-ρ ολταχα',80H
DSO3:
 LDA PPI+2
 ANI 80H
 JZ DSO4
 CALL PRINT
 DW VRAM+1365+78
 DB '2-ρ ολταχα',80H
 JMP DSO5
DSO4:
 CALL PRINT
 DW VRAM+1365+78
 DB '4-ρ ολταχα',80H
DSO5:
 LXI H,NEWSND
 LXI D,OLDSND
 MVI B,3
 MOV A,M
 STAX D
 INX H
 INX D
 DCR B
 JNZ $-6

 RET

SETONC:
 PUSH H
SETON0:
 LXI H,FORM
 LDAX B
 DCR A
 PUSH B
 MOV C,A
 MVI B,0
 DAD B
 POP B
 MOV A,M
 ORA A
 JZ  SETON1
 LXI H,0F743H
 PUSH PSW
 LDA XXX
 DCX H
 DCR A
 JNZ $-2
 POP PSW
 MOV M,A
 POP H
 RET

SETON1:
 INX H
 MOV A,M
 STAX B
 JMP SETON0

DISKEY:
 SUI 24
 PUSH PSW
 ADD A
 MOV C,A
 MVI B,0
 LXI H,KEYS
 DAD B
 MOV E,M
 INX H
 MOV D,M
 XCHG
 LXI B,VRAM+1560
 DAD B
 CALL DRKEY
 LXI H,KEYSGN
 POP PSW
 MOV C,A
 MVI B,0
 DAD B
 MOV A,M
 STAX D
 RET

DISPK:
 SUI 24
 ADD A
 MOV C,A
 MVI B,0
 LXI H,KEYS
 DAD B
 MOV E,M
 INX H
 MOV D,M
 XCHG
 LXI B,VRAM+1560
 DAD B
 MVI M,''
 INX H
 MVI M,''
 INX H
 MVI M,''
 LXI B,76
 DAD B
 MVI M,''
 INX H
 MVI M,''
 INX H
 MVI M,''
 DAD B
 MVI M,''
 INX H
 MVI M,''
 INX H
 MVI M,''
 RET

DISNK:
 ADD A
 MOV C,A
 MVI B,0
 LXI H,NONRES
 DAD B
 MOV E,M
 INX H
 MOV D,M
 XCHG
 LXI B,VRAM+1560
 DAD B
 JMP DRKEY

DRKEY:
 MVI M,''
 INX H
 MVI M,''
 INX H
 MVI M,''
 LXI B,76
 DAD B
 MVI M,''
 INX H
 MOV D,H
 MOV E,L
 INX H
 MVI M,''
 DAD B
 MVI M,''
 INX H
 MVI M,''
 INX H
 MVI M,''
 RET


MULTI: MVI D,0
 MOV L,D
 DAD H
 JNC $+4
 DAD D
 DAD H
 JNC $+4
 DAD D
 DAD H
 JNC $+4
 DAD D
 DAD H
 JNC $+4
 DAD D
 DAD H
 JNC $+4
 DAD D
 DAD H
 JNC $+4
 DAD D
 DAD H
 JNC $+4
 DAD D
 DAD H
 RNC
 DAD D
 RET


GETADR:
 MOV B,H
 MOV C,L
 MOV A,C
 ANA A
 RAR
 MOV H,A
 MVI E,78
 CALL MULTI
 LXI D,VRAM
 DAD D
 MOV A,B
 ANA A
 RAR
 MVI D,0
 MOV E,A
 DAD D
 MOV A,C
 ANA A
 RAR
 MOV A,B
 JC GETDN
 ANI 1
 MVI A,1
 RZ
 MVI A,2
 RET

GETDN:
 ANI 1
 MVI A,4
 RNZ
 MVI A,10H
 RET

PSET: PUSH H
 PUSH B
 PUSH D
 CALL GETADR
 MOV B,M
 ORA B
 MOV M,A
 POP D
 POP B
 POP H
 RET

PRESET: PUSH H
 PUSH B
 PUSH D
 CALL GETADR
 CMA
 MOV B,M
 ANA B
 MOV M,A
 POP D
 POP B
 POP H
 RET

POINT: PUSH H
 PUSH B
 PUSH D
 CALL GETADR
 MOV B,M
 ANA B
 CNZ POINT0
 POP D
 POP B
 POP H
 RET
POINT0: MVI A,0FFH
 RET

PICT:
 MOV A,M
 ORA A
 INX H
 JZ GETVRA
 MOV C,M
 INX H
 MOV B,M
 INX H
 PUSH H
 XCHG
 MOV D,B
 MVI B,0
 PUSH PSW
 MOV A,C
 ORA A
 JP PCT0
 MVI B,0FFH
PCT0:
 POP PSW
PCT1:
 MOV M,A
 DAD B
 DCR D
 JNZ PCT1
 XCHG
 POP H
 JMP PICT
GETVRA:
 MOV E,M
 INX H
 MOV D,M
 MOV A,D
 INX H
 ORA A
 RZ
 JMP PICT


PRINT:
 POP H
 MOV E,M
 INX H
 MOV D,M
 INX H
PRINT0:
 MOV A,M
 ORA A
 JM PRINT1

 STAX D
 INX H
 INX D
 JMP PRINT0
PRINT1:
 ANI 7FH
 STAX D
 INX H
 PCHL


LOUDN:DS 1

WRKARE:
SNDPNT: DS 2
CHR: DS 1
NOTES:DS 3
NUMFRM:DS 3
NEWSND:DS 10
ENDWRK:
OLDSND:DS 3
VOLSND:DS 3
XXX: DS 1

FORM: ; Oηιβαΰύαρ σιηξαμα
;DB 4,4,3,3,3,3,3,4,4,0,1
 DB 10,2,2,2,2,2,3,3,3,3,4,5,6,7,8,9,10,11,12,13,14,15
 DB 16,17,18,20,25,30,100,0,29
 DS 60

KEYBRD: ; ταβμιγα λμαχιατυςω
 DB 00,44,46,00,49,51,00,54,56,58,00,00,40,61,42,41
 DB 38,00,36,45,37,50,00,53,60,31,43,48,00,29,52,34
 DB 30,24,32,28,33,47,39,27,35,25,59,55,00,57,26,00

KEYS:
 DW 702+9
 DW 468+11
 DW 702+13
 DW 468+15
 DW 702+17
 DW 702+21
 DW 468+23
 DW 702+25
 DW 468+27
 DW 702+29
 DW 468+31
 DW 702+33
 DW 702+37
 DW 468+39
 DW 702+41
 DW 468+43
 DW 702+45
 DW 702+49
 DW 468+51
 DW 234+5
 DW 7
 DW 234+9
 DW 11
 DW 234+13
 DW 234+17
 DW 19
 DW 234+21
 DW 23
 DW 234+25
 DW 234+29
 DW 31
 DW 234+33
 DW 35
 DW 234+37
 DW 39
 DW 234+41
 DW 234+45
 DW 47
 DW 234+49

NONRES:
 DW 3
 DW 15
 DW 27
 DW 43
 DW 468+7
 DW 468+19
 DW 468+35
 DW 468+47

KEYSGN:
 DB 'C#D#EF#G#A#BC#D#EF#G#A#BC#D#EF#G#A#BC#D'


 END







